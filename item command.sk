#
# Two /item commands that accept aliases as arguments and are thus extremely versatile.
# E.g. you can use the command like "/item birch plank, 5 red wool and 17 iron ore" and you will receive all 3.
# If you add translated aliases to your aliases.sk your players will be able to use those aliases as well.
#
# Please note that an <items> argument accepts enchantments as well, and only the second example command
# in this script restricts spawing of enchanted items.
aliases:
	blacklisted = bedrock
# This version of the command requires that the player has the permission skript.give,
# and blacklisted items are only allowed if the player has the permission skript.give.bypassblacklist
command /item <items>:
	description: Give yourself some items
	aliases: i
	executable by: players
	permission: skript.give
	trigger:
		if player has permission "skript.give.bypassblacklist":
			give arguments to player
		else:
			loop arguments:
				if loop-item is not blacklisted:
					give loop-item to player
				else:
					message "<red>%loop-item%<reset> is blacklisted and thus cannot be spawned"

# This version of the command checks whether the player has permission to spawn the desired item.
# A player needs the "skript.give.<ID>" permission to spawn an item of the desired type
# and the permission "skript.give.enchanted" to spawn enchanted items.
command /item2 <items>:
	description: Give yourself some items
	usage: /item2 <item(s)>
	aliases: i2
	executable by: players
	permission: skript.give
	trigger:
		loop arguments:
			if loop-item is enchanted:
				if player does not have the permission "skript.give.enchanted":
					message "You don't have permission to spawn enchanted items!"
					stop
			if player has permission "skript.give":
				give loop-item to player
			else:
				message "You don't have permission to spawn <red>%arg-1%<reset>!"


command /give <item types> <player>:
	description: Give someone else some items
	usage: /give <item(s)> to <player>
	permission: skript.give
	trigger:
		loop argument 1:
			if player has permission "skript.give":
				give loop-item to argument 2
				send "You recieved %loop-item% from %player%" to argument 2
			else:
				message "<red>You don't have permission to give away free <red>%loop-item%<reset>!"
